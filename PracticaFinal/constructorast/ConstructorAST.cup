package constructorast;

import java_cup.runtime.*;
import alex.AnalizadorLexicoTiny;
import alex.UnidadLexica;
import alex.TokenValue;
import errors.GestionErroresTiny;

scan with {: return getScanner().next_token(); :};
parser code {: 
   private GestionErroresTiny errores;
   public void syntax_error(Symbol unidadLexica) {
     errores.errorSintactico((UnidadLexica)unidadLexica);
   }
:};
init with {: 
   errores = new GestionErroresTiny();
   AnalizadorLexicoTiny alex = (AnalizadorLexicoTiny)getScanner();
   alex.fijaGestionErrores(errores);
:};


terminal  TokenValue VAR, BOOL, ENTPOS, GUEVARRAY, POINTER, STALINTRUCT, MAIN, LENINT,
                      BOOLSHEVIK, NULL, FUN, TROTSKIF, FIDELSECASTRO, USSWHILE, FORLETARIAT, RED,
                      WRITE, CALL, NEW, DELETE, CONST, IDEN, MAS, MENOS, ASTERISCO, DIV, RESTO, IGUAL,
                      MENORIGUAL, MAYORIGUAL, MENOR, MAYOR, AND, OR, NEG, REF, CABRE, CCIERRA, PUNTO,
                      FLECHA, PABRE, PCIERRA, LLABRE, LLCIERRA, PUNTOYCOMA, ASIGNACION, RETURN, COMA,
                      DISTINTO, TYPE, VOID;

non terminal  Programa, Lista_Instrucciones_Vac, Lista_Instrucciones, Instruccion, 
                InsAsig, InsIf, InsIfElse, InsWhile, InsFor, InsWrite, 
                InsDelete, InsCall, Expr, E0, E1, E2, E3, E4, E5, E6, E7, E8,
                Lista_Param_Vac, Lista_Param,  Bloque, T0, T1, T2, Dec_Var, Dec_Var_Vacia, Dec_Var_Ini,
                Dec_Fun, Dec_Void, Lista_Argumentos_Vac, Lista_Argumentos, Argumento, Dec_Tipo, Dec_Struct,
                CuerpoStruct, Lista_Declaraciones_Vac, Lista_Declaraciones, Declaracion, Declaraciones_Ini_Vac,
                Declaraciones_Ini, Iniciales, Main;

Programa ::= Declaraciones_Ini_Vac Main ;

Main ::= MAIN LLABRE Bloque LLCIERRA ;
Main ::= MAIN error LLCIERRA:l
{: System.out.println("FILA " + l.fila + ", COLUMNA " + l.columna + ": Declaracion de main incorrecta."); :};


Bloque ::= Lista_Declaraciones_Vac Lista_Instrucciones_Vac ;

Lista_Instrucciones_Vac ::= Lista_Instrucciones ;
Lista_Instrucciones_Vac ::= ;
Lista_Instrucciones ::= Lista_Instrucciones Instruccion ;
Lista_Instrucciones ::= Instruccion ;

Lista_Declaraciones_Vac ::= Lista_Declaraciones ;
Lista_Declaraciones_Vac ::= ;
Lista_Declaraciones ::= Lista_Declaraciones Declaracion ;
Lista_Declaraciones ::= Declaracion ;

// Delaraciones iniciales
Declaraciones_Ini_Vac ::= Declaraciones_Ini ;
Declaraciones_Ini_Vac ::= ;
Declaraciones_Ini ::= Declaraciones_Ini Iniciales ;
Declaraciones_Ini ::= Iniciales ;
Iniciales ::= Dec_Fun ;
Iniciales ::= Dec_Void ;
Iniciales ::= Declaracion ;

// Declaraciones en bloques 
Declaracion ::= Dec_Tipo ;
Declaracion ::= Dec_Struct ;
Declaracion ::= Dec_Var ;

// Declaracion de Tipos 
Dec_Tipo ::= TYPE IDEN ASIGNACION T0 PUNTOYCOMA ;
Dec_Tipo ::= TYPE error PUNTOYCOMA:p 
{: System.out.println("FILA " + p.fila + ", COLUMNA " + p.columna + ": Declaracion de tipo incorrecta."); :};

// Declaracion de Structs
Dec_Struct ::= STALINTRUCT IDEN LLABRE CuerpoStruct LLCIERRA ;
Dec_Struct ::= STALINTRUCT error LLCIERRA:l
{: System.out.println("FILA " + l.fila + ", COLUMNA " + l.columna + ": Declaracion de struct incorrecta."); :};
CuerpoStruct ::= Lista_Declaraciones_Vac ;

// Tipos 
T0 ::= POINTER T0 ;
T0 ::= T1 ;
T1 ::= GUEVARRAY T1 CABRE Expr CCIERRA ;
T1 ::= PABRE T0 PCIERRA ;
T1 ::= IDEN ;
T1 ::= T2 ;
T2 ::= LENINT ;
T2 ::= BOOLSHEVIK ;

// Declaracion de variables
Dec_Var ::= Dec_Var_Vacia ;
Dec_Var ::= Dec_Var_Ini ;
Dec_Var ::= VAR error PUNTOYCOMA:p
{: System.out.println("FILA " + p.fila + ", COLUMNA " + p.columna + ": Declaracion de variable incorrecta."); :};
Dec_Var_Vacia ::= VAR T0 IDEN PUNTOYCOMA ;
Dec_Var_Ini ::= VAR T0 IDEN ASIGNACION Expr PUNTOYCOMA ;
Dec_Var_Ini ::= VAR CONST T0 IDEN ASIGNACION Expr PUNTOYCOMA ;

// Declaracion de funciones tipo void
Dec_Void ::= FUN VOID IDEN PABRE Lista_Argumentos_Vac PCIERRA LLABRE Bloque LLCIERRA ;
Dec_Void ::= FUN VOID error LLCIERRA:l
{: System.out.println("FILA " + l.fila + ", COLUMNA " + l.columna + ": Declaracion de funcion incorrecta."); :};

// Declaracion de funciones 
Dec_Fun ::= FUN T0 IDEN PABRE Lista_Argumentos_Vac PCIERRA LLABRE Bloque RETURN Expr PUNTOYCOMA LLCIERRA ;
Dec_Fun ::= FUN error LLCIERRA:l 
{: System.out.println("FILA " + l.fila + ", COLUMNA " + l.columna + ": Declaracion de funcion incorrecta."); :};

Lista_Argumentos_Vac ::= Lista_Argumentos ;
Lista_Argumentos_Vac ::= ;
Lista_Argumentos ::= Lista_Argumentos COMA Argumento ;
Lista_Argumentos ::= Argumento ;
Argumento ::= T0 IDEN ;
Argumento ::= T0 REF IDEN ;


// Instrucciones
Instruccion ::= InsAsig ;
Instruccion ::= InsIf ;
Instruccion ::= InsIfElse ;
Instruccion ::= InsWhile ;
Instruccion ::= InsFor ;
Instruccion ::= InsWrite ;
Instruccion ::= InsDelete ;
Instruccion ::= InsCall ;

InsAsig ::= Expr ASIGNACION Expr PUNTOYCOMA ;
InsIf ::= TROTSKIF PABRE Expr PCIERRA LLABRE Bloque LLCIERRA ;
InsIfElse ::= TROTSKIF PABRE Expr PCIERRA LLABRE Bloque LLCIERRA FIDELSECASTRO LLABRE Bloque LLCIERRA ;
InsIfElse ::= TROTSKIF error LLCIERRA:l
{: System.out.println("FILA " + l.fila + ", COLUMNA " + l.columna + ": Instruccion condicional incorrecta."); :};
InsWhile ::= USSWHILE PABRE Expr PCIERRA LLABRE Bloque LLCIERRA ;
InsWhile ::= USSWHILE error LLCIERRA:l 
{: System.out.println("FILA " + l.fila + ", COLUMNA " + l.columna + ": Instruccion USSWhile incorrecta."); :};
InsFor ::= FORLETARIAT PABRE Dec_Var_Ini Expr PUNTOYCOMA InsAsig PCIERRA LLABRE Bloque LLCIERRA ;
InsFor ::= FORLETARIAT error LLCIERRA:l 
{: System.out.println("FILA " + l.fila + ", COLUMNA " + l.columna + ": Instruccion forLetariat incorrecta."); :};
InsWrite ::= WRITE PABRE Expr PCIERRA PUNTOYCOMA ;
InsDelete ::= DELETE Expr PUNTOYCOMA ;
InsCall ::= CALL IDEN PABRE Lista_Param_Vac PCIERRA PUNTOYCOMA ;
InsCall ::= CALL error PUNTOYCOMA:p
{: System.out.println("FILA " + p.fila + ", COLUMNA " + p.columna + ": Llamada a funcion incorrecta."); :};

// Parametros al llamar una funcion
Lista_Param_Vac ::= Lista_Param;
Lista_Param_Vac ::= ;
Lista_Param::= Lista_Param COMA Expr ;
Lista_Param ::= Expr ;

// Expresiones 
Expr ::= E0 ;

E0 ::= E0 OR E1 ;
E0 ::= E1 ;

E1 ::= E1 AND E2 ;
E1 ::= E2 ;

E2 ::= E2 IGUAL E3 ;
E2 ::= E2 DISTINTO E3 ;
E2 ::= E2 MENORIGUAL E3 ;
E2 ::= E2 MAYORIGUAL E3 ;
E2 ::= E2 MENOR E3 ;
E2 ::= E2 MAYOR E3 ;
E2 ::= E3 ;

E3 ::= E3 MAS E4 ;
E3 ::= E3 MENOS E4 ;
E3 ::= E4 ;

E4 ::= E4 ASTERISCO E5 ;
E4 ::= E4 DIV E5 ;
E4 ::= E4 RESTO E5 ;
E4 ::= E5 ;

E5 ::= MAS E6 ;
E5 ::= MENOS E6 ;
E5 ::= ASTERISCO E6 ;
E5 ::= NEG E6 ;
E5 ::= REF E6 ;
E5 ::= E6 ;

E6 ::= NEW LENINT ;
E6 ::= NEW BOOLSHEVIK ;
E6 ::= RED LENINT ;
E6 ::= RED BOOLSHEVIK ;
E6 ::= E7;

E7 ::= E7 CABRE Expr CCIERRA ;
E7 ::= E7 PUNTO IDEN ;
E7 ::= E7 FLECHA IDEN ;
E7 ::= IDEN PABRE Lista_Param_Vac PCIERRA ;
E7 ::= E8 ; 

E8 ::= ENTPOS ;
E8 ::= BOOL ;
E8 ::= NULL ;
E8 ::= IDEN ;
E8 ::= PABRE Expr PCIERRA ;


